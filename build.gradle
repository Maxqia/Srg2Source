buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "eclipse"
            url = "https://repo.eclipse.org/content/groups/eclipse/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'net.minecraftforge.srg2source'
version = '4.0-SNAPSHOT'
archivesBaseName = 'Srg2Source'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

configurations {
    deployerJars
}

dependencies {
    testCompile "junit:junit:4.12"
    
    compile 'org.ow2.asm:asm-debug-all:5.0.3' // Debug version, so we can have generics!
    compile 'com.google.guava:guava:18.0'
    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'net.sf.jopt-simple:jopt-simple:4.6' // easy CLI parsing
    //compile 'org.eclipse.jgit:org.eclipse.jgit:3.2.0.201312181205-r' // git stuff? may not be used yet
    
    // necessary eclipse AST stuff
    compile 'org.eclipse.core:contenttype:3.4.200-v20130326-1255'
    compile 'org.eclipse.core:jobs:3.5.300-v20130429-1813'
    compile 'org.eclipse.core:runtime:3.9.0-v20130326-1255'
    compile 'org.eclipse.core:resources:3.2.1-R32x_v20060914'
    compile 'org.eclipse:osgi:3.9.1-v20130814-1242'
    compile 'org.eclipse.text:org.eclipse.text:3.5.101'
    compile 'org.eclipse.equinox:common:3.6.200-v20130402-1505'
    compile 'org.eclipse.equinox:preferences:3.5.100-v20130422-1538'
    
    compile 'org.eclipse.jdt:org.eclipse.jdt.core:3.12.0.v20160315-2126'
    
    // Potential Eclipse AST replacement
    compile 'fr.inria.gforge.spoon:spoon-core:5.1.0'
    
    // maven deployer jar
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

repositories {
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "eclipse"
        url = "https://repo.eclipse.org/content/groups/eclipse"
    }
    mavenCentral()
}

jar {
    manifest {
        attributes 'version':project.version, 'javaCompliance': project.targetCompatibility
        attributes 'group':project.group
    }
}

jar {
    exclude 'data/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives shadowJar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {

        dependsOn 'build'

        if (project.hasProperty('forgeMavenPass'))
        {
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: "forge", password: project.getProperty('forgeMavenPass'))
            }
        }
        else
        {
            // local repo folder. Might wanna juset use  gradle install   if you wanans end it to maven-local
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Srg2Source library for ForgeGradle'
                url 'https://github.com/MinecraftForge/Srg2Source'

                scm {
                    url 'https://github.com/MinecraftForge/Srg2Source'
                    connection 'scm:git:git://github.com/MinecraftForge/Srg2Source.git'
                    developerConnection 'scm:git:git@github.com:MinecraftForge/Srg2Source.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/MinecraftForge/Srg2Source/issues'
                }

                licenses {
                    license {
                        name 'The BSD 3-Clause License'
                        url 'http://opensource.org/licenses/BSD-3-Clause'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'agaricusb'
                        name 'Agaricusb'
                        roles { role 'developer' }
                    }
                    developer {
                        id 'LexManos'
                        name 'Lex Manos'
                        roles { role 'developer' }
                    }
                    developer {
                        id 'AbrarSyed'
                        name 'Abrar Syed'
                        roles { role 'developer' }
                    }
                }
            }
        }
    }
}
